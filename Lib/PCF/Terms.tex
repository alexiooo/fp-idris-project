\subsection{Implementing Terms}

\begin{hidden}
module Lib.PCF.Terms

import public Lib.PCF.Types

import public Data.Fin  -- needed publically, since we publically export types that reference Fin
import public Data.Vect

%default total
\end{hidden}

We now want to define terms. We use de Bruijn indices to represent bound variables.
This is an elegant way to deal with alpha-equivalence.

\begin{code}
public export
Var : Nat -> Type
Var = Fin
\end{code}

Instead of having a ton of different constructors, we define a symbol data type
and then define a single constructor that works for all symbols.

\begin{code}
namespace Symbol
  public export
  data Symbol : (0 ar : Nat) -> Type where
    IfElse : Symbol 3       -- if-then-else construct
    App    : Symbol 2       -- application
    Pair   : Symbol 2       -- pairing
    Fst    : Symbol 1       -- first projection
    Snd    : Symbol 1       -- second projection
    Succ   : Symbol 1       -- successor
    Pred   : Symbol 1       -- predecessor
    IsZero : Symbol 1       -- is zero predicate
    Y      : Symbol 1       -- fixpoint / Y-combinator
    T      : Symbol 0       -- true
    F      : Symbol 0       -- false
    Zero   : Symbol 0       -- zero value
    Unit   : Symbol 0       -- unit value (*)
\end{code}

We also keep track of (an upper bound on) free variables in the type:
\lstinline{PCFTerm n} encodes terms with at most $n$ free variables.

\begin{code}
public export
data PCFTerm : Nat -> Type where
  V    : Var k -> PCFTerm k                             -- variables
  L    : PCFType   -> PCFTerm (S k) -> PCFTerm k        -- lambda
  S    : Symbol ar -> Vect ar (PCFTerm k) -> PCFTerm k  -- other symbols
\end{code}

Of special interest are the closed terms, those without any free variables

\begin{code}
public export
ClosedPCFTerm : Type
ClosedPCFTerm = PCFTerm 0
\end{code}

Remember that the type only gives an upper bound, so an inhabitant of say
\lstinline{PCFType 3} might still be closed.

The following will try to strengthen any such term. This really is just a
wrapper around Fin.strengthen, with straightforward recursive cases,
so we detail only variables and lambdas.

\begin{code}
strengthen : {k : _} -> PCFTerm (S k) -> Maybe (PCFTerm k)
strengthenVect : {k : _} -> Vect n (PCFTerm (S k)) -> Maybe (Vect n (PCFTerm k))
\end{code}

The \lstinline{strengthenVect} function is useful for the symbol case.

\begin{code}
strengthen (V v)    = Fin.strengthen v >>= Just . V
strengthen (L t m)  = strengthen m     >>= Just . L t
strengthen (S s ar) = Just (S s !(strengthenVect ar))

strengthenVect (m::ms) = [| (strengthen m) :: (strengthenVect ms) |]
strengthenVect []      = Just []
\end{code}

\begin{code}
public export
tryClose : {k : _} -> PCFTerm k -> Maybe ClosedPCFTerm
tryClose {k} t = case k of
                 0      => Just t
                 (S k') => strengthen t >>= tryClose
\end{code}

As for types, we want terms to be comparable. The important case is lambda-abstraction.
We are using de Bruijn indices, which make comparing terms very easy.
We once again skip the other trivial cases.

\begin{hidden}
implementation Eq (Symbol k) where
  IfElse == IfElse = True
  App    == App    = True
  Pair   == Pair   = True
  Fst    == Fst    = True
  Snd    == Snd    = True
  Succ   == Succ   = True
  Pred   == Pred   = True
  IsZero == IsZero = True
  Y      == Y      = True
  T      == T      = True
  F      == F      = True
  Zero   == Zero   = True
  Unit   == Unit   = True
  _      == _      = False
\end{hidden}

\begin{code}
public export partial
implementation Eq (PCFTerm k) where
  V v         == V w          = v == w
  L a m       == L b n        = a == b && m == n
\end{code}

\begin{hidden}
  S s [a]     == S p [x]      = s == p && a == x
  S s [a,b]   == S p [x,y]    = s == p && a == x && b == y
  S s [a,b,c] == S p [x,y,z]  = s == p && a == x && b == y && c == z
  _           == _            = False
\end{hidden}

We can also implement a show function. The implementation in itself is not
interesting, so we omit the details.

\begin{hidden}
public export
Show (PCFTerm k) where
  show (V x)                = show x
  show (L t m)              = "(λ" ++ show t ++ "." ++ show m ++ ")"
  show (S IfElse [p, m, n]) = "if " ++ show p ++ " then " ++ show m ++ " else " ++ show n
  show (S App    [m,n])     = "(" ++ show m ++ show n ++ ")"
  show (S Pair   [m,n])     = "<" ++ show m ++ ", " ++ show n ++ ">"
  show (S Fst    [m])       = "π₁(" ++ show m ++ ")"
  show (S Snd    [m])       = "π₂(" ++ show m ++ ")"
  show (S Succ   [m])       = "succ (" ++ show m ++ ")"
  show (S Pred   [m])       = "pred (" ++ show m ++ ")"
  show (S IsZero [m])       = "iszero (" ++ show m ++ ")"
  show (S Y      [m])       = "Y(" ++ show m ++ ")"
  show (S T      _)         = "T"
  show (S F      _)         = "F"
  show (S Zero   _)         = "zero"
  show (S Unit   _)         = "*"
\end{hidden}
