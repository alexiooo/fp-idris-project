\subsection{Reduction}

\begin{hidden}
module Lib.PCF.Reduction

import public Lib.PCF.Terms
import public Lib.PCF.Types
import public Lib.PCF.Substitute
import public Lib.PCF.TypeOf

import Data.Fuel

%default total
\end{hidden}

We can now define reduction. We begin with small-step reduction.

\subsubsection{Small-step Reduction}

Not all terms can reduce, it is thus important that the result is of type Maybe PCFTerm.

\begin{code}
public export
smallStep : {k : _} -> PCFTerm k -> Maybe (PCFTerm k)
\end{code}

Neither variables nor top-level lambdas are reducable, so we restrict attention to the symbols

\begin{code}
smallStep (S s arg) = ssSym s arg where
  ssSym : Symbol ar -> Vect ar (PCFTerm k) -> Maybe (PCFTerm k)
\end{code}

We start with the top-level reducts. It is worth mentioning that the reduction
rule for $\mbf{pred}$ is slightly different from the one exposed in section %%% Add section number!!

\begin{code}
  ssSym Pred [S Zero []]     = Just $ S Zero []
  ssSym Pred [S Succ [m]]    = Just m

  ssSym IsZero [S Zero _]    = Just $ S T []
  ssSym IsZero [S Succ _]    = Just $ S F []

  ssSym App [(L _ m), n]     = Just (substitute n m)

  ssSym Fst [S Pair [m, _]]  = Just m
  ssSym Snd [S Pair [_, n]]  = Just n

  ssSym IfElse [S T _, m, _] = Just m
  ssSym IfElse [S F _, _, n] = Just n

  ssSym Y [m]                = Just $ S App [m, (S Y [m])]
\end{code}

If a term had no top-level reducts, then we try to reduce its first argument.
The only exception is Pair, which does not evaluate its arguments at all.

\begin{code}
  ssSym Pair _    = Nothing
  ssSym s (m::ms) = Just $ S s (!(smallStep m) :: ms)
\end{code}

Alternatively, if the term is of the unit type, but not the unit value itself, we reduce it to the
unit value

\begin{code}
  ssSym Unit _ = Nothing
  ssSym s arg  = let m = (S s arg) in
                 case tryClose m >>= typeOfClosed of
                   Just PCFUnit => Just (S Unit [])
                   _            => Nothing

smallStep _ = Nothing
\end{code}

\subsubsection{Values}

A certain subset of terms are called \emph{values}. Those include constants,
successors of values, pairs and abstractions.

\begin{code}
public export
data PCFValue : Nat -> Type where
  C     : Symbol 0   -> PCFValue k
  Succ  : PCFValue k -> PCFValue k
  Pair  : PCFTerm k  -> PCFTerm k     -> PCFValue k
  L     : PCFType    -> PCFTerm (S k) -> PCFValue k
\end{code}

Of course we are mainly interested in closed values.

\begin{code}
public export
ClosedPCFValue : Type
ClosedPCFValue = PCFValue 0
\end{code}

Some terms are values. We want to be able to convert those into the \lstinline{PCFValue} type.

\begin{code}
public export
toValue : PCFTerm k -> Maybe (PCFValue k)
toValue (L t m)         = Just $ L t m
toValue (S s [])        = Just $ C s
toValue (S Succ [m])    = Just $ Succ !(toValue m)
toValue (S Pair [m, n]) = Just $ Pair m n
toValue _               = Nothing
\end{code}

Conversely, all values are terms, and we must be able to convert \lstinline{PCFValues}
to \lstinline{PCFTerms}.

\begin{code}
public export
toTerm : PCFValue k -> PCFTerm k
toTerm (C s)      = S s []
toTerm (Succ v)   = S Succ [toTerm v]
toTerm (Pair m n) = S Pair [m, n]
toTerm (L t m)    = L t m
\end{code}

Finally, we want to check whether or not a term is a value.

\begin{code}
public export
isValue : PCFTerm k -> Bool
isValue (L t m)         = True
isValue (S s [])        = True
isValue (S Succ [m])    = isValue m
isValue (S Pair [m, n]) = True
isValue _               = False
\end{code}

Values are exactly the normal forms for small-step reduction, that is, values
are the terms that cannot be reduced further.

\subsubsection{Big-step Reduction}

By successively applying small-step reductions, terms can reduce to values.
This is the so called big-step reduction.

Not all PCFTerms evaluate to a Value, some may enter an infinite loop.
To still define eval as a total function, we supply it with some `fuel'.
This fuel acts as an upper bound on computation steps. For well-typed terms,
the function will only return Nothing if this upper bound was reached.

\begin{code}
public export
eval : Fuel -> ClosedPCFTerm -> Maybe ClosedPCFValue
\end{code}

To begin with, values reduce to themselves.

\begin{code}
eval _ (L t m)         = Just $ L t m
eval _ (S Pair [m, n]) = Just $ Pair m n
eval _ (S s [])        = Just $ C s
\end{code}

Terms starting with Y are the terms that might not terminate.
Evaluating such terms cost fuel.

\begin{code}
eval (More f) (S Y [m]) = eval f (S App [m, S Y [m]])
\end{code}

For othercases, we just do one step of computation then keep evaluating.

\begin{code}
eval f (S s (m :: ms)) = evSym s !(eval f m) ms where
  evSym : Symbol (1 + ar) -> ClosedPCFValue -> Vect ar ClosedPCFTerm -> Maybe ClosedPCFValue
  evSym IfElse  (C T)      [m, _] = eval f m
  evSym IfElse  (C F)      [_, n] = eval f n
  evSym Pred    (C Zero)   _      = Just $ C Zero
  evSym Pred    (Succ m)   _      = Just m
  evSym IsZero  (C Zero)   _      = Just $ C T
  evSym IsZero  (Succ _)   _      = Just $ C F
  evSym App     (L _ m)    [n]    = eval f (substitute n m)
  evSym Fst     (Pair m _) _      = eval f m
  evSym Snd     (Pair _ n) _      = eval f n
  evSym s       v          ms     = let m = (S s ((toTerm v) :: ms)) in  -- take the evaluated first arg, and recombine into a term m
                 case typeOfClosed m of
                    Just PCFUnit => Just (C Unit)
                    -- everything that hasn't evaluated so far, will evaluates to itself, but only
                    -- if it is representable as value. This should be the case for all well-typed
                    -- terms that reach this point (in fact, every well-typed term that reaches this
                    -- point should be a successor). Notice that we are using the evaluated first
                    -- argument here, not the original one.
                    _            => toValue m
\end{code}
